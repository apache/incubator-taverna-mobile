apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.butterknife'
apply from: '../config/quality/quality.gradle'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    useLibrary 'org.apache.http.legacy'
    lintOptions {
        abortOnError false

    }
    defaultConfig {
        applicationId "org.apache.taverna.mobile"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        testInstrumentationRunner 'org.apache.taverna.mobile.runner.RxAndroidJUnitRunner'
    }

    sourceSets {
        def commonTestDir = 'src/commonTest/java'

        main {
            java.srcDirs = ['src/main/java']
        }
        androidTest {
            java.srcDirs = ['src/androidTest/java/']
            java.srcDir commonTestDir
            resources.srcDirs += 'src/commonTest/resources'

        }
        test {
            java.srcDirs = ['src/test/java/']
            java.srcDir commonTestDir
            resources.srcDirs += 'src/commonTest/resources'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }

    buildToolsVersion rootProject.ext.buildToolsVersion
}
repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    //Dependencies for support library
    implementation "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
    implementation "com.android.support:cardview-v7:$rootProject.supportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$rootProject.supportLibraryVersion"
    implementation "com.android.support:support-v4:$rootProject.supportLibraryVersion"
    implementation "com.android.support:support-annotations:$rootProject.supportLibraryVersion"

    implementation "com.android.support:design:$rootProject.supportLibraryVersion"
    implementation "com.android.support:preference-v7:$rootProject.supportLibraryVersion"
    implementation "com.android.support:preference-v14:$rootProject.supportLibraryVersion"

    //Dependencies for retofit and okhhtp3
    implementation "com.squareup.retrofit2:retrofit:$rootProject.retrofitVersionLatest"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$rootProject.retrofitVersionLatest"
    implementation "com.squareup.okhttp3:logging-interceptor:$rootProject.okHttp3Version"
    implementation "com.squareup.retrofit2:converter-gson:$rootProject.retrofitVersionLatest"

    implementation("com.squareup.retrofit2:converter-simplexml:$rootProject.retrofitVersionLatest") {
        exclude group: 'xpp3', module: 'xpp3'
        exclude group: 'stax', module: 'stax-api'
        exclude group: 'stax', module: 'stax'
    }

    //Dependencies for butterknife
    implementation "com.jakewharton:butterknife:$rootProject.butterKnifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$rootProject.butterKnifeVersion"

    implementation "io.reactivex.rxjava2:rxjava:$rootProject.rxJava2Version"
    implementation "io.reactivex.rxjava2:rxandroid:$rootProject.rxJava2Version"

    implementation("org.simpleframework:simple-xml:2.7.1") {
        exclude module: 'stax'
        exclude module: 'stax-api'
        exclude module: 'xpp3'
    }

    implementation "com.android.support:customtabs:$rootProject.supportLibraryVersion"
    implementation 'com.github.bumptech.glide:glide:3.6.0'
    implementation 'com.caverock:androidsvg:1.2.1'

    //Dependencies for dbflow
    annotationProcessor "com.github.Raizlabs.DBFlow:dbflow-processor:$rootProject.raizLabsDBFlow"
    implementation "com.github.Raizlabs.DBFlow:dbflow-core:$rootProject.raizLabsDBFlow"
    implementation "com.github.Raizlabs.DBFlow:dbflow:$rootProject.raizLabsDBFlow"

    //Dependencies for db debug
    implementation "com.facebook.stetho:stetho:$rootProject.stetho"
    implementation "com.facebook.stetho:stetho-okhttp3:$rootProject.stetho"

    //Dependencies for Image zoom
    implementation "com.github.chrisbanes:PhotoView:1.2.6"
    implementation "com.android.support:multidex:1.0.2"
    implementation "com.google.code.gson:gson:2.8.2"

    implementation 'de.hdodenhof:circleimageview:2.2.0'
    implementation "com.anton46:stepsview:0.0.2"

    //Dependencies for LeakCanary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$rootProject.leakcanary"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$rootProject.leakcanary"
    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$rootProject.leakcanary"

    //Dependencies for JUNit and unit tests.
    testImplementation "junit:junit:4.12"
    testImplementation "org.mockito:mockito-core:2.8.47"

    androidTestImplementation "junit:junit:4.12"
    androidTestImplementation "org.mockito:mockito-core:2.8.47"

    androidTestImplementation "org.mockito:mockito-android:2.8.47"
    androidTestImplementation "com.android.support:support-annotations:$rootProject.supportLibraryVersion"
    androidTestImplementation("com.android.support.test.espresso:espresso-contrib:$rootProject.espresso") {
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$rootProject.espresso"
    androidTestImplementation ("com.android.support.test.espresso:espresso-intents:$rootProject.espresso")

    androidTestImplementation "com.android.support.test:runner:$rootProject.runner"
    androidTestImplementation "com.android.support.test:rules:$rootProject.runner"

    //Dependencies for Dagger 2
    implementation  "com.google.dagger:dagger:$rootProject.dagger"
    testImplementation  "com.google.dagger:dagger:$rootProject.dagger"
    androidTestImplementation  "com.google.dagger:dagger:$rootProject.dagger"
    annotationProcessor "com.google.dagger:dagger-compiler:$rootProject.dagger"
    androidTestAnnotationProcessor "com.google.dagger:dagger-compiler:$rootProject.dagger"
    testAnnotationProcessor "com.google.dagger:dagger-compiler:$rootProject.dagger"

}
