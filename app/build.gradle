apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.butterknife'
apply from: '../config/quality/quality.gradle'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    useLibrary 'org.apache.http.legacy'
    lintOptions {
        abortOnError false

    }
    defaultConfig {
        applicationId "org.apache.taverna.mobile"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
    }

    sourceSets {
        def commonTestDir = 'src/commonTest/java'

        main {
            java.srcDirs = ['src/main/java']
        }
        androidTest {
            java.srcDirs = ['src/androidTest/java/']
            java.srcDir commonTestDir
        }
        test {
            java.srcDirs = ['src/test/java/']
            java.srcDir commonTestDir
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }

    buildToolsVersion rootProject.ext.buildToolsVersion
}
repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    //Dependencies for support library
    compile "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:cardview-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:recyclerview-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:support-v4:$rootProject.supportLibraryVersion"

    compile "com.android.support:design:$rootProject.supportLibraryVersion"
    compile "com.android.support:preference-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:preference-v14:$rootProject.supportLibraryVersion"

    //Dependencies for retofit and okhhtp3
    compile "com.squareup.retrofit2:retrofit:$rootProject.retrofitVersionLatest"
    compile "com.squareup.retrofit2:adapter-rxjava:$rootProject.retrofitVersionLatest"
    compile "com.squareup.okhttp3:logging-interceptor:$rootProject.okHttp3Version"
    compile "com.squareup.retrofit2:converter-gson:$rootProject.retrofitVersionLatest"

    compile("com.squareup.retrofit2:converter-simplexml:$rootProject.retrofitVersionLatest") {
        exclude group: 'xpp3', module: 'xpp3'
        exclude group: 'stax', module: 'stax-api'
        exclude group: 'stax', module: 'stax'
    }

    //Dependencies for butterknife
    compile "com.jakewharton:butterknife:$rootProject.butterKnifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$rootProject.butterKnifeVersion"


    compile "io.reactivex:rxandroid:1.2.0"
// Because RxAndroid releases are few and far between, it is recommended you also
// explicitly depend on RxJava's latest version for bug fixes and new features.
    compile "io.reactivex:rxjava:1.1.5"

    compile("org.simpleframework:simple-xml:2.7.+") {
        exclude module: 'stax'
        exclude module: 'stax-api'
        exclude module: 'xpp3'
    }

    compile 'com.github.bumptech.glide:glide:3.6.0'
    compile 'com.caverock:androidsvg:1.2.1'

    //Dependencies for dbflow
    annotationProcessor "com.github.Raizlabs.DBFlow:dbflow-processor:$rootProject.raizLabsDBFlow"
    compile "com.github.Raizlabs.DBFlow:dbflow-core:$rootProject.raizLabsDBFlow"
    compile "com.github.Raizlabs.DBFlow:dbflow:$rootProject.raizLabsDBFlow"

    //Dependencies for db debug
    compile "com.facebook.stetho:stetho:$rootProject.stetho"
    compile "com.facebook.stetho:stetho-okhttp3:$rootProject.stetho"

    //Dependencies for Image zoom
    compile "com.github.chrisbanes:PhotoView:1.2.6"
    compile "com.android.support:multidex:1.0.2"
    compile "com.google.code.gson:gson:2.8.2"

    compile "com.anton46:stepsview:0.0.2"

    //Dependencies for LeakCanary
    debugCompile "com.squareup.leakcanary:leakcanary-android:$rootProject.leakcanary"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$rootProject.leakcanary"
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:$rootProject.leakcanary"

    //Dependencies for JUNit and unit tests.
    testCompile "junit:junit:4.12"
    testCompile "org.mockito:mockito-all:1.10.19"

}
